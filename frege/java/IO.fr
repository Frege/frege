{--
    Classes from java.io
-}

protected package frege.java.IO where

import frege.java.Lang public(IOException, PrintStream)
import frege.prelude.PreludeBase
import frege.prelude.PreludeIO
import frege.prelude.PreludeText
import frege.prelude.PreludeList
import frege.prelude.PreludeMonad
import frege.prelude.PreludeArrays

data FileNotFoundException = pure native java.io.FileNotFoundException
derive Exceptional FileNotFoundException

data UnsupportedEncodingException = pure native java.io.UnsupportedEncodingException
derive Exceptional UnsupportedEncodingException

data EOFException = pure native java.io.EOFException where
    pure native new :: String -> EOFException
derive Exceptional EOFException


--- frege equivalent of @java.io.OutputStream@
data OutputStream = mutable native java.io.OutputStream

--- frege equivalent of @java.io.FileOutputStream@
data FileOutputStream = mutable native java.io.FileOutputStream where
    --- > FileOutputStream.new file true 
    --- writes to the end of a file rather than the beginning
    native new :: MutableIO File -> IO FileOutputStream
                    throws FileNotFoundException
                | MutableIO File -> Bool -> IO FileOutputStream
                    throws FileNotFoundException
                | String -> IO FileOutputStream
                    throws FileNotFoundException
                | String -> Bool -> IO FileOutputStream
                    throws FileNotFoundException
    --- Writes the specified *byte* to this file output stream.  
    native write :: FileOutputStream -> Int -> IO ()
                    throws IOException
                                                

--- frege equivalent of @java.io.Closeable@
data Closeable = mutable native java.io.Closeable where    
    native close :: Closeable -> IO () throws IOException

--- frege equivalent of @java.io.Flushable@
data Flushable = mutable native java.io.Flushable where
    native flush :: Flushable -> IO () throws IOException

--- forward declaration of URI
protected data URI = pure native java.net.URI

data File = native java.io.File where
    native new                :: String -> STMutable s File
                              |  Mutable s File -> String -> STMutable s File
    pure native toURI         :: File -> URI
    pure native getPathF  getPath    :: File -> String
    
    --- Separator for elements of a path name, i.e. "/" on Unix
    pure native separator        java.io.File.separator
                              :: String
    --- Separator for elements of a path, i.e. ":" on Unix
    pure native pathSeparator    java.io.File.pathSeparator
                              :: String
    --- The (relative) path name. Not necessarily valid.
    native getPath       :: MutableIO File -> IO String
    native getName       :: MutableIO File -> IO String
    native canRead       :: MutableIO File -> IO Bool
    native canWrite      :: MutableIO File -> IO Bool
    native isAbsolute    :: MutableIO File -> IO Bool
    native isDirectory   :: MutableIO File -> IO Bool
    native isFile        :: MutableIO File -> IO Bool
    native exists        :: MutableIO File -> IO Bool
    native mkdirs        :: MutableIO File -> IO Bool
    native delete        :: MutableIO File -> IO Bool
    native renameTo      :: MutableIO File -> MutableIO File -> IO Bool
    native lastModified  :: MutableIO File -> IO Long
    native getParentFile :: MutableIO File -> IO (Maybe (MutableIO File))
    native list          :: MutableIO File -> IO (Maybe (MutableIO (JArray String)))
    --- Create an empty file in the default temp directory.
    --- > createTempFile "abc" ".suffix"
    --- The prefix must be at least 3 characters long!
    native createTempFile java.io.File.createTempFile
                         :: String -> String -> IOMutable File
                                                throws IOException

instance Serializable File

instance Show File where show = File.getPathF

data Writer = mutable native java.io.Writer where
    native write :: Writer -> Int -> IO () throws IOException
                 |  Writer -> String -> IO () throws IOException
                 |  Writer -> String -> Int -> Int -> IO () throws IOException
    putChar :: Writer -> Char -> IO ()
    putChar w c = write w (ord c)

data OutputStreamWriter = mutable native java.io.OutputStreamWriter where
    native new :: OutputStream -> String -> IO OutputStreamWriter
                                throws UnsupportedEncodingException

data PrintWriter = mutable native java.io.PrintWriter where
    --- print a 'String'
    native print    :: PrintWriter -> String -> IO ()
    --- print a 'String' followed by a line terminator, or just a line terminator
    native println  :: PrintWriter -> String -> IO ()
                    |  PrintWriter -> IO ()
    --- format and print 1 to 9 values, see 'String.format' 
    native printf   :: PrintWriter -> String -> a -> IO ()
                    |  PrintWriter -> String -> a -> b -> IO ()
                    |  PrintWriter -> String -> a -> b -> c -> IO ()
                    |  PrintWriter -> String -> a -> b -> c -> d -> IO ()
                    |  PrintWriter -> String -> a -> b -> c -> d -> e -> IO ()
                    |  PrintWriter -> String -> a -> b -> c -> d -> e -> f -> IO ()
                    |  PrintWriter -> String -> a -> b -> c -> d -> e -> f -> g -> IO ()
                    |  PrintWriter -> String -> a -> b -> c -> d -> e -> f -> g -> h -> IO ()
                    |  PrintWriter -> String -> a -> b -> c -> d -> e -> f -> g -> h -> i -> IO ()
    native new      :: String -> IO PrintWriter throws FileNotFoundException
                    |  MutableIO File -> IO PrintWriter throws FileNotFoundException
                    |  MutableIO File -> String -> IO PrintWriter 
                                throws FileNotFoundException, UnsupportedEncodingException
                    |  Writer -> IO PrintWriter
                    |  Writer -> Bool -> IO PrintWriter

--- nowarn: Don't warn because of constantness
--- The standard output 'PrintWriter'
native stdout "frege.runtime.Runtime.stdout.get()" :: PrintWriter

--- nowarn: Don't warn because of constantness
--- The standard error 'PrintWriter'
native stderr "frege.runtime.Runtime.stderr.get()" :: PrintWriter

--- nowarn: Don't warn because of constantness
--- The standard input 'BufferedReader'
native stdin  "frege.runtime.Runtime.stdin.get()"  :: BufferedReader
        
{-- 
    Frege type for a @java.io.StringWriter@
    
    Not intended for direct use but rather as something
    a 'PrintWriter' can be made of. (Though, because
    of the 'Appendable' inheritance, one could 
    still 'Appendable.append' directly.)
    
    To be used like:
    
    > action :: PrintWriter -> IO ()
    > action =  ...
    > actionOnStringWriter :: IO String  
    > actionOnStringWriter = do
    >       sw <- StringWriter.new
    >       pr <- sw.printer
    >       action pr
    >       pr.close
    >       sw.toString  
    -}    
data StringWriter = mutable native java.io.StringWriter where
    --- create a fresh 'StringWriter'
    native new      :: () -> IO StringWriter
    --- get the content of a 'StringWriter' as 'String'    
    native toString :: StringWriter -> IO String
    --- make a 'PrintWriter' that prints to this 'StringWriter'
    printer :: StringWriter -> IO PrintWriter
    printer this = PrintWriter.new this -- IOMut PrintWriter

            
-- ----------------------------------------------------------------------------
-- Input Streams & Readers
-- ----------------------------------------------------------------------------

data InputStream = mutable native java.io.InputStream

data FileInputStream = mutable native java.io.FileInputStream where
    native new :: MutableIO File -> IO FileInputStream 
                    throws FileNotFoundException
                | String  -> IO FileInputStream 
                    throws FileNotFoundException

data Reader = mutable native java.io.Reader where
    {--
        Reads a single character and returns it as 'Int' in the range 0 to 65535.
        Returns -1 if the end of the stream has been reached.
        
        Throws 'IOException' if an I/O error occurs.
    -}
    native read :: Reader -> IO Int throws IOException
    
    {--
        Read the next character from the input stream.
        Throws 'EOFException' if the end of the stream has been reached, or 'IOException' if
        an I/O error occurs.
    -}
    getChar :: Reader -> IO Char
    getChar rdr = do
        i <- rdr.read
        if i < 0 then throwIO (EOFException.new "getChar") else return (chr i) 

data InputStreamReader = mutable native java.io.InputStreamReader where
    native new :: InputStream -> String -> IO InputStreamReader
                    throws UnsupportedEncodingException
    
data BufferedReader = mutable native java.io.BufferedReader where
    native new :: Reader -> IO BufferedReader
    {--
        Reads a line of text. A line is considered to be terminated 
        by any one of a line feed ("\n"), a carriage return ("\r"), 
        or a carriage return followed immediately by a linefeed.
        
        [Returns:] 'Just' _string_, where _string_ is the contents of the line, 
        not including any line-termination characters, 
        or 'Nothing' if the end of the stream has been reached.

        [Throws:] IOException - If an I/O error occurs
    -}
    native readLine :: BufferedReader -> IO (Maybe String)
                    throws IOException
    --- read all lines and return them as list, close reader afterwards
    getLines :: BufferedReader -> IO [String]
    getLines br = go []  where
        go acc = do
            xms <- br.readLine
            case xms of
                Just s ->  go (s:acc) 
                _      ->  br.close >> return (reverse acc)

    {-- 
        Reads the next line from a buffered reader using 'BufferedReader.readLine', 
        and returns the string or throws 'EOFException' on end of file. 
        -}       
    getLine :: BufferedReader -> IO String
    getLine br = readLine br >>= maybe (throwIO (EOFException.new "getLine")) return
    

{-- 
    Convenience function to open a file and wrap it with an UTF-8 decoding
    buffered 'Reader'.
    
    May throw 'FileNotFoundException'
    -}

openReader :: String -> IO BufferedReader
openReader file = do
    fis <- FileInputStream.new file
    isr <- InputStreamReader.new fis "UTF-8"                    
    BufferedReader.new isr

{--
    Convenience function to open a file for writing through an UTF-8 encoding
    'PrintWriter'.
    
    May throw 'FileNotFoundException'
    -}
openWriter :: String -> IO PrintWriter
openWriter name = do
    file <- File.new name
    PrintWriter.new file "UTF-8"

{--
    Convenience function to open a file in append mode for 
    writing through an UTF-8 encoding
    'PrintWriter'.
    
    May throw 'FileNotFoundException'
    -}
appendWriter :: String -> IO PrintWriter
appendWriter name = do
        fos <- FileOutputStream.new name true
        ofw <- OutputStreamWriter.new fos "UTF-8"
        PrintWriter.new ofw