{-- 

    Support for Java reflective arrays.

-}
protected module frege.prelude.PreludeArrays where

import frege.prelude.PreludeBase
import frege.prelude.PreludeList() 
import frege.prelude.PreludeList as L(ListSource, length, map, fold, zipWith, null, zip) 
import frege.prelude.Maybe (mapMaybe)
import frege.prelude.PreludeMonad
import frege.prelude.PreludeIO
import frege.control.Semigroupoid
import frege.control.Category

--- short hand for 'Mutable' @s@ ('JArray' @a@)
type ArrayOf s a = Mutable s (JArray a)

{--
    The type @JArray X@ corresponds to Java's @J[]@ for any type @X@
    where @J@ is the corresponding java type of @X@,
    but note that Java does not really have generic arrays.
    
    We can use arrays generically inside Frege, but native code 
    that expects or returns arrays will not be generic.  
    
    The native interface will take every occurrence of
    
    > JArray X
    
    or
    
    > Mutable s (JArray X)
    
    in native declarations to denote the corresponding Java array type.
    But when the type argument is variable, it will be just @Object@.
    This corresponds to the usage in 
    'http://docs.oracle.com/javase/7/docs/api/index.html?java/lang/reflect/Array.html the array reflection API'.

    Multi-dimensional arrays are not very well supported out of the box.
    If one needs more than 1 dimensions, the type will get quite complex,
    as of course arrays are mutable and so one will have multiple levels
    of 'JArray' nested in 'Mutable' nested in 'JArray' and so forth.
    Moreover, multi-dimensional arrays cannot be generic at all.
    
    Note that there are really 2 different APIs: 
    
    1. With the 'JArray.getElemAt', 'JArray.getAt', 'JArray.setAt', 'JArray.setElemAt',
    'JArray.itemAt' and 'JArray.elemAt' it is possible to work on Java objects with
    java compile time type @java.lang.Object@ *or* @X[]@ for some java type @X@
    *or* @java.lang.Object[]@, 
    but the run time type is always @java.lang.Object[]@. 
    2. With the 'newArray', 'getElemAt', 'getAt', 'setAt', 
    'setElemAt', 'itemAt' and 'elemAt' functions of the 'ArrayElem' class
    for some type @F@ that is an instance of 'ArrayElem', 
    we can work on Java objects with
    Java compile time type @X[]@, 
    where @X@ is the Java type corresponding to @F@,    
    but the run time type is always @X[]@.    
    
    The former ones are usable only in polymorphic functions where the type argument 
    for 'JArray' is a variable and we don't (want to) have 'ArrayElem' constraints. 
    They are not good for interfacing native methods that take or return arrays of a certain type.
    Run time type errors are possible because native methods could put anything there.
    However, when used in Frege only, the typing is safe.
    
    The latter ones are truly type safe, because their Frege type corresponds to the
    expected Java compile time type, which is also the actual run time type.  
    
    Here is a cheat sheet for the different array get and set methods:
    
    >             Array type            Argument/    Description
    >                                   Result
     
    > setAt       Mutable (JArray s X)  Maybe X     set null or data element
    > setElemAt   Mutable (JArray s X)  X           set data element
    > getAt       Mutable (JArray s X)  Maybe X     get null or data element
    > getElemAt   Mutable (JArray s X)  X           get data element (unsafe)
    > itemAt      JArray s X            Maybe X     get null or data element (pure)
    > elemAt      JArray s X            X           get data element (pure, unsafe)
    
    "unsafe" in this context applies only to non-primitive types
    and means that the function will fail with a @NullPointerException@
    if the value accessed is a Java @null@.

-}
data JArray a = native "java.lang.reflect.Array" where

    ---  create a one dimensional Java array 
    native new   java.lang.reflect.Array.newInstance :: Class a -> Int -> STMutable s (JArray a)
    
 
    --- create a one dimensional Java array that holds 'Mutable' data
    native newM  java.lang.reflect.Array.newInstance :: Class a -> Int -> STMutable s (JArray (Mutable s a))
    
    {-- 
        Tell the length of an immutable Java array.
        
        Because the length of an array cannot change, 
        it is safe to use this function with 'readonly'. 
    -}  
    pure native length  java.lang.reflect.Array.getLength :: JArray a -> Int
    
    --- Return the length of a mutable array in the 'ST' monad.
    getLength :: ArrayOf Î± Î² -> ST Î± Int
    getLength = readonly length
    
    {--
        Get the array element at a given index. This will throw an
        'IndexOutOfBoundsException' if the index is lower than 0 or
        greater or equal to the length of the array.
        
        Because in general, array elements may be @null@, the return
        value is wrapped in 'Maybe', as usual.
    -}
    pure native itemAt  java.lang.reflect.Array.get  :: JArray a -> Int -> Maybe a
    
    {--
        Like 'JArray.itemAt' but the result is not wrapped in 'Maybe'.
        
        This is appropriate for arrays of primitive values.
        For all other arrays, the user is expected to prove that the
        element cannot be @null@ or else risk a @NullPointerException@.
    -}
    pure native elemAt  java.lang.reflect.Array.get  :: JArray a -> Int -> a
    
    {--
        Get the array element at a certain index of a mutable array and
        return it in the 'ST' monad.
        
        This will throw an
        'IndexOutOfBoundsException' if the index is lower than 0 or
        greater or equal to the length of the array.
        
        Because in general, array elements may be @null@, the return
        value is wrapped in 'Maybe', as usual.
    -}
    native       getAt  java.lang.reflect.Array.get  :: Mutable s (JArray a) -> Int -> ST s (Maybe a)
    
    {--
        Set the element at a certain index of a mutable array to a value
        that is wrapped in 'Maybe'. This won't work for primitive element types.

        This will throw an
        'IndexOutOfBoundsException' if the index is lower than 0 or
        greater or equal to the length of the array.
        
        To set the corresponding array element to @null@, pass 'Nothing',
        otherwise pass a 'Just' value.  
    -}
    native       setAt  java.lang.reflect.Array.set  :: Mutable s (JArray a) -> Int -> Maybe a -> ST s ()

    {-- 
        Get the array element at a certain index of a mutable array and
        return it in the 'ST' monad.
        
        This will throw an
        'IndexOutOfBoundsException' if the index is lower than 0 or
        greater or equal to the length of the array.

        Unlike with 'JArray.getAt' the element *must not be @null@*.
                        
        This is appropriate for arrays of primitive values.
        For all other arrays, the user is expected to prove that the
        element cannot be @null@ or else risk a @NullPointerException@.
    -} 
    native   getElemAt  java.lang.reflect.Array.get  :: Mutable s (JArray a) -> Int -> ST s a

    {--
        Set the element at a certain index of a mutable array.

        This will throw an
        'IndexOutOfBoundsException' if the index is lower than 0 or
        greater or equal to the length of the array. 
    -}
    native   setElemAt  java.lang.reflect.Array.set  :: Mutable s (JArray a) -> Int -> a -> ST s ()

    {--
        Create a mutable array from a finite list.            
    -}
    fromList :: ArrayElem Î± => [Î±] -> STMutable Î² (JArray Î±)
    fromList xs = do
        let !len = xs.length 
        arr <- newArray len
        zipWithM_ (ArrayElem.setElemAt arr) [0..len-1] xs
        return arr

    {--
        Create a mutable array from a finite index/value list.

        Indexes not mentioned in the list remain @null@ for 
        non primitive array elements and 0 otherwise.            
    -}
    fromIndexList :: ArrayElem Î± => [(Int,Î±)] -> STMutable Î² (JArray Î±)
    fromIndexList xs = do
        let !len = L.fold max 0 (L.map fst xs) 
        arr <- newArray (if null xs then 0 else len+1)
        mapM_ (\(i,a) -> ArrayElem.setElemAt arr i a)  xs
        return arr

    {--
        Create a mutable array from a finite list of 'Maybe' values.
    -}
    fromMaybeList âˆ· ArrayElem Î± â‡’ [Maybe Î±] -> STMutable Î² (JArray Î±)
    fromMaybeList xs = fromIndexList ys
        where
            ys = [ (n;y) | (Just y, n) <- zip xs [0..]]
            
    {--
        Create a mutable generic array from a finite index/value list.
        
        Indexes not mentioned in the list remain @null@ for 
        non primitive array elements and 0 otherwise.            
    -}
    -- genericFromIndexList :: JavaType Î± => [(Int,Î±)] -> STMutable Î² (JArray Î±)
    genericFromIndexList xs = do
        let !len = L.fold max 0 (L.map fst xs) 
        arr <- JArray.new javaClass (if null xs then 0 else len+1)
        mapM_ (uncurry (JArray.setElemAt arr))  xs
        return arr

    {--
        Apply a function to an array element and 
        write the result to another array at the same index.
        
        If the target array is a primitive one, 
        there must be no null elements in the source array.
    -}
    mapElem :: (ArrayElem Î³, ArrayElem Î±) => (Î³->Î±) -> JArray Î³ -> ArrayOf Î² Î± -> Int -> ST Î² ()
    mapElem f src dest index = ArrayElem.setAt dest index value 
        where value = fmap f (ArrayElem.itemAt src index) 
    
    --- Map a function over the elements of an immutable array.
    --- The results will be collected in a new mutable array of the same length.
    --- If the target element type is a primitive one, no *null* elements are allowed in the source array.
    map :: (ArrayElem Î±, ArrayElem Î²) => (Î±->Î²) -> JArray Î± -> STMutable Î³ (JArray Î²)
    map f src = do
            let !max = length src
            dest  <- newArray max
            mapM_ (JArray.mapElem f src dest) [0..max-1]
            return dest
    
    --- Modify a mutable array by applying a function to all its elements.
    modify :: ArrayElem Î± => (Î±->Î±) -> ArrayOf Î² Î± -> ST Î² ()
    modify f dest = do
            max <- dest.getLength
            mapM_ (modifyAt f dest) [0..max-1]
    
    --- Equivalent of 'fold' for mutable arrays. 
    fold :: ArrayElem Î± => (Î²->Î±->Î²) -> Î² -> ArrayOf Î³ Î± -> ST Î³ Î²
    fold f acc arr = arr.getLength >>= foldM collect acc . enumFromTo 0 . pred
        where
            collect acc i = ArrayElem.getAt arr i >>= return . maybe acc (f acc)      
    
    {--
        Create a mutable array of a given size and compute the values of its elements
        by some function. The function gets the current index and  
        the already computed values in  the form of an immutable array,
        where it can access elements with a *smaller* index than the current one. 
        
        The restriction to smaller indexes is because array elements are
        strict in Frege. For example, we can't store unevaluated values in
        an @int[]@ array, because the Java type of unevaluated values is not @int@.
        
        To create an array of 1000 fibonacci numbers, one could write:
        
        > cache fib 1000 where
        >   fib 0 _ = 1n
        >   fib 1 _ = 1n
        >   fib n a = a.[n-1] + a.[n-2]
    -}
    cache f n = do
                arr <- newArray n
                mapM_ (\i -> readonly (f i) arr >>= setElemAt arr i) [0..n-1]
                return arr


instance ListSource JArray where
    --- Unload an immutable array to a list
    --- The resulting list consists of all the non null elements of the array argument.
    toList :: JArray a -> [a]
    toList !ra = mapMaybe ra.itemAt [0..ra.length-1] 

instance Eq (Eq a) â‡’ JArray a where
    a == b
        | a.length == b.length = go (a.length-1)
        | otherwise = false
        where
            go n | n >= 0, JArray.itemAt a n == JArray.itemAt b n = go (n-1)
                 | otherwise = n < 0
    hashCode  = arrayFold (\a\b -> (31*a) + hashCode b) 1 

--- Unload an immutable array to a list.
--- The non-null elements become 'Just' values, the @null@s translate to 'Nothing'
toMaybeList :: JArray Î± -> [Maybe Î±]
toMaybeList ja = map ja.itemAt [0..ja.length-1]

--- Create an immutable array from a finite list whose elements are 'Maybe' values.
--- Uses 'JArray.fromMaybeList' and freezes the resulting array.
--- Note that 
--- > arrayFromList . toList
--- does not guarantee reconstruction of the original array, because @null@s get lost, whereas
--- > arrayFromMaybeList . toMaybeList
--- does.
arrayFromMaybeList âˆ· ArrayElem ð–† â‡’ [Maybe ð–†] â†’ JArray ð–†
arrayFromMaybeList !xs = (JArray.fromMaybeList xs >>= readonly id).run

--- Create an immutable array from a finite list whose elements are 'ArrayElem`
--- Uses 'JArray.fromList' and freezes the resulting array.
arrayFromList :: ArrayElem Î± => [Î±] -> JArray Î±
arrayFromList !xs = (JArray.fromList xs >>= readonly id).run

--- Create an immutable array from a finite index/value list.
--- Uses 'JArray.fromIndexList' and freezes the resulting array.    
arrayFromIndexList :: ArrayElem Î± => [(Int, Î±)] -> JArray Î±
arrayFromIndexList !xs = (JArray.fromIndexList xs >>= readonly id).run    

--- Create an immutable generic array from a finite index/value list.
--- Uses 'JArray.genericFromIndexList' and freezes the resulting array.
--  (This is used in the parsers generated with YYGen) 
genericArrayFromIndexList :: JavaType Î± => [(Int, Î±)] -> JArray Î±
genericArrayFromIndexList !xs = (JArray.genericFromIndexList xs >>= readonly id).run    



{-- 
    Map a function over the elements of an immutable array,
    and collect the results in another immutable array.

    Uses 'JArray.map' and makes result read-only. 
-}
arrayMap :: (ArrayElem Î±, ArrayElem Î²) => (Î±->Î²) -> JArray Î± -> JArray Î²
arrayMap f arr = ST.run (JArray.map f arr >>= readonly id)

{--
    Left fold an immutable array
-}
arrayFold :: (Î±->Î²->Î±) -> Î± -> JArray Î² -> Î±
arrayFold !f !acc arr = go acc 0
    where
        go !acc n 
            | n < arr.length = case JArray.itemAt arr n of
                    Just b  â†’ go (f acc b) (n+1)
                    Nothing â†’ go acc (n+1)
            | otherwise = acc


{--
    Memoize a number of results from a function that maps
    'Int' to the array element.
    
    Uses 'JArray.cache' and makes it immutable
    -}
arrayCache f n = (JArray.cache f n >>= readonly id).run


{--
    Type class for basic 'JArray' operations. The *element type*
    must be an instance of this class to support arrays of that type.
    
    'ArrayElem' is not derivable, derive sub-class 'ArrayElement' instead.
    
    The operations are mostly overloaded on return type and provide the 
    appropriate @java.lang.Class@ object when needed. 
    
    This supports one dimensional arrays, though more dimensions would
    be possible with some extra effort.
    
    Note that 'JArray' cannot be an instance of 'ArrayElem' itself,
    because it has no fixed @java.lang.Class@ instance.
-}
class ArrayElem JavaType a => a where
    --- Create a one dimensional array with elements of the instantiated type.
    native newArray "new[]"   :: Int -> ST s (ArrayOf s a)
    -- newArray :: Int -> ST s (ArrayOf s a)
    -- Create a two dimensional array with elements of the instantiated type.
    -- new2DArray  :: Int -> Int -> ST s (ArrayOf s (ArrayOf s a))
    --- Get item at index from immutable array, see 'JArray.itemAt'
    pure native itemAt  "[i]"   :: JArray a -> Int -> Maybe a
    --- Get non-null element at index from immutable array, see 'JArray.elemAt'
    pure native elemAt  "[i]"   :: JArray a -> Int -> a
    --- Get item at index from mutable array, see 'JArray.getAt'
    native getAt        "[i]"   :: Mutable s (JArray a) -> Int -> ST s (Maybe a)
    --- Set item or null at index in mutable array, see 'JArray.setAt'
    native setAt        "[]="   :: Mutable s (JArray a) -> Int -> Maybe a -> ST s ()
    --- Get non null item at index from mutable array, see 'JArray.getElemAt'
    native getElemAt    "[i]"   :: Mutable s (JArray a) -> Int -> ST s a
    --- Set item at index in mutable array. see 'JArray.setElemAt'
    native setElemAt    "[]="   :: Mutable s (JArray a) -> Int -> a -> ST s ()
    --- Modify item at index in mutable array with result of function application.
    modifyAt    :: (a -> a) -> Mutable s (JArray a) -> Int -> ST s ()
    modifyAt f arr i = getAt arr i >>= setAt arr i . fmap f 
    --- Modify non null item at index in mutable array with result of function application.
    modifyElemAt    :: (a -> a) -> Mutable s (JArray a) -> Int -> ST s ()
    modifyElemAt f arr i = getElemAt arr i >>= setElemAt arr i . f 
    
    -- newArray n = JArray.new javaClass n
    

{-- 
    Type class for array elements of primitive type.
    
    Not thought for public use, as all instances are pre-defined.
    
    The default implementation of 'PrimitiveArrayElement.setAt'
    does not support passing 'Nothing', because there can be no
    @null@ in primitive arrays.
-}
class PrimitiveArrayElement ArrayElem a => a where
    --- Default implementation suitable for primitive types.
    --- It is an error to put 'Nothing' in a primitive array.
    setAt arr inx = setElemAt arr inx . maybe (error "cannot have null in primitive arrays") id
                    
    --- Default implementation suitable for primitive types, wraps result with 'Just'
    getAt arr inx = getElemAt arr inx >>= return . Just
    --- Default implementation suitable for primitive types, wraps result with 'Just'
    itemAt arr inx = Just (elemAt arr inx)


--- 'ArrayElement' is the class one derives for array elements.
--- In addition to the base class 'ArrayElem' it also supports mutable array elements.
class ArrayElement ArrayElem a => a where
    --- Create an array of mutable items. 
    --- Such an array may never be immutable itself. 
    newArrayM  :: Int -> ST s (ArrayOf s (Mutable s a))
    newArrayM n = JArray.newM javaClass n
    
    
-- Instances for primitive array elements. 
-- These use the specialised getXXX/setXXX function from java.lang.reflect.Array

instance PrimitiveArrayElement Int where
    native javaClass "int.class" :: Class Int

instance PrimitiveArrayElement Long where
    native javaClass "long.class" :: Class Long

instance PrimitiveArrayElement Char where
    native javaClass "char.class" :: Class Char

instance PrimitiveArrayElement Bool where
    native javaClass "boolean.class" :: Class Bool
        
instance PrimitiveArrayElement Float where
    native javaClass "float.class" :: Class Float

instance PrimitiveArrayElement Double where
    native javaClass "double.class" :: Class Double

instance ArrayElement (String) where
    native javaClass "java.lang.String.class" :: Class String 

instance ArrayElement (a -> b) where
    native javaClass "frege.runtime.Lambda.class" :: Class (a -> b)

derive ArrayElement [a] 
derive ArrayElement Integer
-- derive ArrayElement (Maybe a)
-- derive ArrayElement (Either a b)
derive ArrayElement (a,b)
derive ArrayElement (a,b,c)

instance JavaType (JArray Int) where
    pure native javaClass "int[].class" :: Class (JArray Int) 

--darr = JArray.genericFromIndexList ([] :: [(Int, JArray Int)])
